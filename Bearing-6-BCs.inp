/COM,===========================================================================
/COM,BOUNDARY CONDITIONS
/COM,===========================================================================

/PREP7

/COM,---------------------------------------------------------------------------
/COM,TEMPERATURE
/COM,---------------------------------------------------------------------------

*IF,heating_sensors,EQ,2,THEN
	! Temperature difference 25 C
	*SET,temperature_inner_ring,110
	*SET,temperature_outer_ring,85
*ELSEIF,heating_sensors,EQ,1,THEN
	! Temperature difference 50 C
	*SET,temperature_inner_ring,110
	*SET,temperature_outer_ring,60
*ELSEIF,heating_sensors,EQ,0,THEN
	! Temperature difference 80 C
	*SET,temperature_inner_ring,150
	*SET,temperature_outer_ring,70
*ENDIF

! Temperature of ball
*IF,temperature_switch,EQ,room_temperature,THEN
	*SET,temperature_ball,20
*ELSEIF,temperature_switch,EQ,min_temperature,THEN
	*SET,temperature_ball,MIN(temperature_inner_ring,temperature_outer_ring)
*ELSEIF,temperature_switch,EQ,max_temperature,THEN
	*SET,temperature_ball,MAX(temperature_inner_ring,temperature_outer_ring)
*ELSEIF,temperature_switch,EQ,average_temperature,THEN
	*SET,temperature_ball,1/2*(temperature_inner_ring+temperature_outer_ring)
*ENDIF

! Reference temperature
TREF,20

! Uniform temperature inner ring
CMSEL,S,inring_volume,VOLU
BFV,ALL,TEMP,temperature_inner_ring

! Uniform temperature outer ring
CMSEL,S,outring_volume,VOLU
BFV,ALL,TEMP,temperature_outer_ring

*IF,ignore_ball,NE,true,THEN

	! Uniform temperature ball
	CMSEL,S,ball_volume,VOLU
	BFV,ALL,TEMP,temperature_ball

*ENDIF

/COM,---------------------------------------------------------------------------
/COM,AXIAL DISPLACEMENT
/COM,---------------------------------------------------------------------------

! No displacement in axial direction
CSYS,0
*IF,mirror_symmetry,EQ,false,AND,add_self_weight,EQ,true,THEN
	CMSEL,S,inring_volume,VOLU
	NSLV,S,1
	NSEL,R,LOC,Z,-inring_thickness/2,-inring_thickness/2
*ELSE
	NSEL,S,LOC,Z,0,0
*ENDIF
D,ALL,UZ,0
ALLSEL,ALL

/COM,---------------------------------------------------------------------------
/COM,CYCLIC SYMMETRY
/COM,---------------------------------------------------------------------------

! No displacement in tangential direction on the ball side
CSYS,1
NSEL,S,LOC,Y,0,0
D,ALL,UY,0
NSEL,ALL

! No displacement in tangential direction on the other side
LOCAL,200,CYLIN,0,0,0,ball_sector_angle/2,0,0
NSEL,S,LOC,Y,0,0
DSYM,SYMM,Y,200
NSEL,ALL

/COM,---------------------------------------------------------------------------
/COM,WEIGHT
/COM,---------------------------------------------------------------------------

*IF,mirror_symmetry,EQ,false,AND,add_self_weight,EQ,true,THEN
	ACEL,0,0,-9.8*meter/(second**2)
*ENDIF

ALLSEL,ALL
