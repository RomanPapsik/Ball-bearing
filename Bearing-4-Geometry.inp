/COM,===========================================================================
/COM,GEOMETRY
/COM,===========================================================================

/PREP7
/FOCUS,1,ball_centre_radius,0,0

/COM,---------------------------------------------------------------------------
/COM,DIMENSIONS
/COM,---------------------------------------------------------------------------

! Inner ring
*SET,inring_thickness      ,65.0*millimeter
*SET,inring_diameter_small ,150.0*millimeter
*SET,inring_diameter_groove,187.375*millimeter
*SET,inring_diameter_large ,206.4*millimeter
*SET,inring_groove_radius  ,24.41*millimeter
*SET,inring_groove_centre  ,inring_diameter_groove+2*inring_groove_radius
*SET,inring_groove_depth   ,(inring_diameter_large-inring_diameter_groove)/2
*SET,inring_groove_width   ,2*inring_groove_radius*SIN(ACOS(1-inring_groove_depth/inring_groove_radius))
*SET,inring_chamfer        ,4*millimeter

! Outer ring
*SET,outring_thickness      ,65.0*millimeter
*SET,outring_diameter_small ,265.3*millimeter
*SET,outring_diameter_groove,282.625*millimeter
*SET,outring_diameter_large ,320*millimeter
*SET,outring_groove_radius  ,24.65*millimeter
*SET,outring_groove_centre  ,outring_diameter_groove-2*outring_groove_radius
*SET,outring_groove_depth   ,(outring_diameter_groove-outring_diameter_small)/2
*SET,outring_groove_width   ,2*outring_groove_radius*SIN(ACOS(1-outring_groove_depth/outring_groove_radius))
*SET,outring_chamfer        ,4*millimeter

! Ball
*SET,ball_diameter,47.625*millimeter
*SET,ball_radius,ball_diameter/2
*SET,ball_centre_radius,(inring_diameter_groove+outring_diameter_groove)/4

! Sector
*SET,balls_count,9
*SET,ball_sector_angle,360/balls_count
*SET,half_sector_angle,ball_sector_angle/2

! Meridian planes
*DIM,meridian_angles,ARRAY,3
*SET,meridian_angles(1),0,half_sector_angle/3,half_sector_angle

/COM,---------------------------------------------------------------------------
/COM,INNER RING
/COM,---------------------------------------------------------------------------

CSYS,1
NUMSTR,LINE,101 $ NUMSTR,AREA,101 $ NUMSTR,VOLU,101

*DO,i,1,6
	*IF,i,LE,3,THEN
		*SET,angle,meridian_angles(i) $ *SET,k,+1
	*ELSE
		*SET,angle,meridian_angles(i-3) $ *SET,k,-1
	*ENDIF
	K,1%i%0,inring_groove_centre/2                          ,angle,0
	K,1%i%1,inring_diameter_groove/2                        ,angle,0
	K,1%i%2,inring_diameter_groove/4+inring_diameter_small/4,angle,0
	K,1%i%3,inring_diameter_small/2                         ,angle,0
	K,1%i%4,inring_diameter_small/2                         ,angle,k*(inring_thickness/2-inring_chamfer)
	K,1%i%5,inring_diameter_small/2+inring_chamfer          ,angle,k*(inring_thickness/2)
	K,1%i%6,inring_diameter_large/2-inring_chamfer          ,angle,k*(inring_thickness/2)
	K,1%i%7,inring_diameter_large/2                         ,angle,k*(inring_thickness/2-inring_chamfer)
	K,1%i%8,inring_diameter_large/2                         ,angle,k*(inring_groove_width/2)
	K,1%i%9,inring_diameter_small/4+inring_diameter_large/4 ,angle,k*(inring_groove_width/4+inring_thickness/4-inring_chamfer/2)
	LARC,1%i%8,1%i%1,1%i%0,inring_groove_radius
*ENDDO

V,111,112,119,118, 121,122,129,128 $ *REPEAT,2,10,10,10,10,10,10,10,10
V,112,113,114,119, 122,123,124,129 $ *REPEAT,2,10,10,10,10,10,10,10,10
V,114,115,116,119, 124,125,126,129 $ *REPEAT,2,10,10,10,10,10,10,10,10
V,116,117,118,119, 126,127,128,129 $ *REPEAT,2,10,10,10,10,10,10,10,10
*IF,mirror_symmetry,EQ,false,THEN
V,141,142,149,148, 151,152,159,158 $ *REPEAT,2,10,10,10,10,10,10,10,10
V,142,143,144,149, 152,153,154,159 $ *REPEAT,2,10,10,10,10,10,10,10,10
V,144,145,146,149, 154,155,156,159 $ *REPEAT,2,10,10,10,10,10,10,10,10
V,146,147,148,149, 156,157,158,159 $ *REPEAT,2,10,10,10,10,10,10,10,10
*ENDIF

VATT,CrMnSi
NUMMRG,KP,micrometer/10,,,LOW

/COM,---------------------------------------------------------------------------
/COM,OUTER RING
/COM,---------------------------------------------------------------------------

CSYS,1
NUMSTR,LINE,201 $ NUMSTR,AREA,201 $ NUMSTR,VOLU,201

*DO,i,1,6
	*IF,i,LE,3,THEN
		*SET,angle,meridian_angles(i) $ *SET,k,+1
	*ELSE
		*SET,angle,meridian_angles(i-3) $ *SET,k,-1
	*ENDIF
	K,2%i%0,outring_groove_centre/2                           ,angle,0
	K,2%i%1,outring_diameter_groove/2                         ,angle,0
	K,2%i%2,outring_diameter_groove/4+outring_diameter_large/4,angle,0
	K,2%i%3,outring_diameter_large/2                          ,angle,0
	K,2%i%4,outring_diameter_large/2                          ,angle,k*(outring_thickness/2-outring_chamfer)
	K,2%i%5,outring_diameter_large/2-outring_chamfer          ,angle,k*(outring_thickness/2)
	K,2%i%6,outring_diameter_small/2+outring_chamfer          ,angle,k*(outring_thickness/2)
	K,2%i%7,outring_diameter_small/2                          ,angle,k*(outring_thickness/2-outring_chamfer)
	K,2%i%8,outring_diameter_small/2                          ,angle,k*(outring_groove_width/2)
	K,2%i%9,outring_diameter_small/4+outring_diameter_large/4 ,angle,k*(outring_groove_width/4+outring_thickness/4-outring_chamfer/2)
	LARC,2%i%1,2%i%8,2%i%0,outring_groove_radius
*ENDDO

V,211,212,219,218, 221,222,229,228 $ *REPEAT,2,10,10,10,10,10,10,10,10
V,212,213,214,219, 222,223,224,229 $ *REPEAT,2,10,10,10,10,10,10,10,10
V,214,215,216,219, 224,225,226,229 $ *REPEAT,2,10,10,10,10,10,10,10,10
V,216,217,218,219, 226,227,228,229 $ *REPEAT,2,10,10,10,10,10,10,10,10
*IF,mirror_symmetry,EQ,false,THEN
V,241,242,249,248, 251,252,259,258 $ *REPEAT,2,10,10,10,10,10,10,10,10
V,242,243,244,249, 252,253,254,259 $ *REPEAT,2,10,10,10,10,10,10,10,10
V,244,245,246,249, 254,255,256,259 $ *REPEAT,2,10,10,10,10,10,10,10,10
V,246,247,248,249, 256,257,258,259 $ *REPEAT,2,10,10,10,10,10,10,10,10
*ENDIF

VATT,CrMnSi
NUMMRG,KP,micrometer/10,,,LOW

/COM,---------------------------------------------------------------------------
/COM,BALL
/COM,---------------------------------------------------------------------------

*IF,ignore_ball,EQ,false,THEN

LOCAL,300,SPHE,ball_centre_radius,0,0,0,0
K,3000,0,0,0

K,3111,ball_diameter/4,  0, 90 $ K,3211,ball_diameter/2,  0, 90
K,3112,ball_diameter/4,  0, 50 $ K,3212,ball_diameter/2,  0, 50
K,3113,ball_diameter/4,  0,  0 $ K,3213,ball_diameter/2,  0,  0
K,3114,ball_diameter/4,  0,-50 $ K,3214,ball_diameter/2,  0,-50
K,3115,ball_diameter/4,  0,-90 $ K,3215,ball_diameter/2,  0,-90
K,3122,ball_diameter/4, 40, 50 $ K,3222,ball_diameter/2, 40, 50
K,3123,ball_diameter/4, 40,  0 $ K,3223,ball_diameter/2, 40,  0
K,3124,ball_diameter/4, 40,-50 $ K,3224,ball_diameter/2, 40,-50
K,3132,ball_diameter/4, 90, 50 $ K,3232,ball_diameter/2, 90, 50
K,3133,ball_diameter/4, 90,  0 $ K,3233,ball_diameter/2, 90,  0
K,3134,ball_diameter/4, 90,-50 $ K,3234,ball_diameter/2, 90,-50
K,3142,ball_diameter/4,140, 50 $ K,3242,ball_diameter/2,140, 50
K,3143,ball_diameter/4,140,  0 $ K,3243,ball_diameter/2,140,  0
K,3144,ball_diameter/4,140,-50 $ K,3244,ball_diameter/2,140,-50
K,3152,ball_diameter/4,180, 50 $ K,3252,ball_diameter/2,180, 50
K,3153,ball_diameter/4,180,  0 $ K,3253,ball_diameter/2,180,  0
K,3154,ball_diameter/4,180,-50 $ K,3254,ball_diameter/2,180,-50

NUMSTR,LINE,301 $ NUMSTR,AREA,301 $ NUMSTR,VOLU,301
VSEL,NONE

CSKP,300,SPHE,3000,3113,3133
V, 3113,3123,3122,3112, 3213,3223,3222,3212
V, 3123,3133,3132,3122, 3223,3233,3232,3222
V, 3133,3143,3142,3132, 3233,3243,3242,3232
V, 3143,3153,3152,3142, 3243,3253,3252,3242
CSKP,300,SPHE,3000,3113,3111
V, 3000,3113,3112,3111, 3133,3123,3122,3132
V, 3111,3112,3212,3211, 3132,3122,3222,3232
V, 3000,3133,3132,3111, 3153,3143,3142,3152
V, 3111,3211,3232,3132, 3152,3252,3242,3142

*IF,mirror_symmetry,EQ,false,THEN

CSKP,300,SPHE,3000,3153,3133
V, 3153,3143,3144,3154, 3253,3243,3244,3254
V, 3143,3133,3134,3144, 3243,3233,3234,3244
V, 3133,3123,3124,3134, 3233,3223,3224,3234
V, 3123,3113,3114,3124, 3223,3213,3214,3224
CSKP,300,SPHE,3000,3153,3115
V, 3000,3153,3154,3115, 3133,3143,3144,3134
V, 3115,3154,3254,3215, 3134,3144,3244,3234
V, 3000,3113,3114,3115, 3133,3123,3124,3134
V, 3115,3114,3214,3215, 3134,3124,3224,3234

*ENDIF ! mirror_symmetry,EQ,true

VATT,Si3N4

*ENDIF ! ignore_ball,EQ,false

/COM,---------------------------------------------------------------------------
/COM,COMPONENTS
/COM,---------------------------------------------------------------------------

! Inner ring volume
VSEL,S,VOLU,,100,199
CM,inring_volume,VOLU

! Outer ring volume
VSEL,S,VOLU,,200,299
CM,outring_volume,VOLU

! Ball volume
VSEL,S,VOLU,,300,399
CM,ball_volume,VOLU

! Inner ring contact surface facing ball
KSEL,S,KP,,111,121,10 $ KSEL,A,KP,,118,128,10 $ KSEL,A,KP,,148,158,10 $ LSLK,S,1 $ ASLL,S,1
CM,inring_facing_ball,AREA

! Ball contact surface facing inner ring
KSEL,S,KP,,3242,3244 $ KSEL,A,KP,,3252,3254 $ LSLK,S,1 $ ASLL,S,1
CM,ball_facing_inring,AREA

! Ball contact surface facing outer ring
KSEL,S,KP,,3212,3214 $ KSEL,A,KP,,3222,3224 $ LSLK,S,1 $ ASLL,S,1
CM,ball_facing_outring,AREA

! Outer ring contact surface facing ball
KSEL,S,KP,,211,221,10 $ KSEL,A,KP,,218,228,10 $ KSEL,A,KP,,248,258,10 $ LSLK,S,1 $ ASLL,S,1
CM,outring_facing_ball,AREA

ALLSEL,ALL
VPLOT
