/COM,===========================================================================
/COM,MESH
/COM,===========================================================================

/PREP7

/COM,---------------------------------------------------------------------------
/COM,DIMENSIONS
/COM,---------------------------------------------------------------------------

*SET,inring_thickness      ,65.0*millimeter
*SET,inring_diameter_small ,150.0*millimeter
*SET,inring_diameter_groove,187.375*millimeter
*SET,inring_diameter_large ,206.4*millimeter
*SET,inring_groove_radius  ,24.41*millimeter
*SET,inring_groove_centre  ,inring_diameter_groove+2*inring_groove_radius
*SET,inring_groove_depth   ,(inring_diameter_large-inring_diameter_groove)/2
*SET,inring_groove_width   ,2*inring_groove_radius*SIN(ACOS(1-inring_groove_depth/inring_groove_radius))
*SET,inring_chamfer        ,4*millimeter

*SET,outring_thickness      ,65.0*millimeter
*SET,outring_diameter_small ,265.3*millimeter
*SET,outring_diameter_groove,282.625*millimeter
*SET,outring_diameter_large ,320*millimeter
*SET,outring_groove_radius  ,24.65*millimeter
*SET,outring_groove_centre  ,outring_diameter_groove-2*outring_groove_radius
*SET,outring_groove_depth   ,(outring_diameter_groove-outring_diameter_small)/2
*SET,outring_groove_width   ,2*outring_groove_radius*SIN(ACOS(1-outring_groove_depth/outring_groove_radius))
*SET,outring_chamfer        ,4*millimeter

/COM,---------------------------------------------------------------------------
/COM,INNER RING
/COM,---------------------------------------------------------------------------

CSYS,0
NUMSTR,AREA,101 $ NUMSTR,VOLU,101

K,100,inring_groove_centre/2,0
K,101,inring_diameter_groove/2,0
K,102,inring_diameter_small/2,0
K,103,inring_diameter_small/2,inring_thickness/2-inring_chamfer
K,104,inring_diameter_small/2+inring_chamfer,inring_thickness/2
K,105,inring_diameter_large/2-inring_chamfer,inring_thickness/2
K,106,inring_diameter_large/2,inring_thickness/2-inring_chamfer
K,107,inring_diameter_large/2,inring_groove_width/2
LARC,101,107,100,inring_groove_radius

A,101,102,103,104,105,106,107
AATT,CrMnSi

! /COM,---------------------------------------------------------------------------
! /COM,OUTER RING
! /COM,---------------------------------------------------------------------------

CSYS,0
NUMSTR,AREA,201 $ NUMSTR,VOLU,201

K,200,outring_groove_centre/2,0
K,201,outring_diameter_groove/2,0
K,202,outring_diameter_large/2,0
K,203,outring_diameter_large/2,outring_thickness/2-outring_chamfer
K,204,outring_diameter_large/2-outring_chamfer,outring_thickness/2
K,205,outring_diameter_small/2+outring_chamfer,outring_thickness/2
K,206,outring_diameter_small/2,outring_thickness/2-outring_chamfer
K,207,outring_diameter_small/2,outring_groove_width/2
LARC,201,207,200,outring_groove_radius

A,201,202,203,204,205,206,207
AATT,CrMnSi

/COM,---------------------------------------------------------------------------
/COM,STRUCTURAL MESH
/COM,---------------------------------------------------------------------------

! Structural 20-node element
*SET,element_structural,183
ET,element_structural,PLANE183

! Element formulation
*SET,axi_symmetric,1
*SET,generalized_plane_strain,5
KEYOPT,element_structural,3,axi_symmetric

TYPE,element_structural
MSHAPE,hexahedral_mesh,2D
MSHKEY,free_mesh

! Element size - big for testing
ESIZE,0.5*millimeter

! Mesh inner ring
AMESH,101
! Mesh outer ring
AMESH,201

/COM,===========================================================================
/COM,BOUNDARY CONDITIONS
/COM,===========================================================================

/PREP7

nsel,s,loc,y,0,0
D,ALL,UY,0

! Number of sensors determines temperature
*SET,heating_sensors,2

*IF,heating_sensors,EQ,2,THEN
	! Temperature difference 25 C
	*SET,temperature_inner_ring,110
	*SET,temperature_outer_ring,85
*ELSEIF,heating_sensors,EQ,1,THEN
	! Temperature difference 50 C
	*SET,temperature_inner_ring,110
	*SET,temperature_outer_ring,60
*ELSEIF,heating_sensors,EQ,0,THEN
	! Temperature difference 80 C
	*SET,temperature_inner_ring,150
	*SET,temperature_outer_ring,70
*ENDIF

*SET,temperature_ball,(temperature_inner_ring+temperature_outer_ring)/2


/COM,---------------------------------------------------------------------------
/COM,TEMPERATURE
/COM,---------------------------------------------------------------------------

! Reference temperature
TREF,20

! Uniform temperature inner ring
BFA,101,TEMP,temperature_inner_ring

! Uniform temperature outer ring
BFA,201,TEMP,temperature_outer_ring

! TUNIF,temperature_inner_ring

/COM,===========================================================================
/COM,SOLUTION
/COM,===========================================================================

/SOLU

! New analysis
ANTYPE,STATIC,NEW

! Save all nodal data for every substep
OUTRES,ALL,ALL

! Direct sparse solver
EQSLV,SPAR
DSPOPTION,,INCORE

! Large deformations
NLGEOM,OFF

! Time stepping
AUTOTS,OFF

! Select everything
ALLSEL,ALL

! Solve
/UIS,MSGPOP,3
SOLVE
/UIS,MSGPOP,0

/COM,===========================================================================
/COM,RESULTS
/COM,===========================================================================

/POST1

/CMAP
/CONTOUR,1,100

RSYS,5

/VIEW,1,0,0,1
/ANGLE,1,0

PLNSOL,U,X

! Make background white
/RGB,INDEX,100,100,100,0
! Make lines black
/RGB,INDEX,0,0,0,15
 ! Change output to file
/SHOW,PNG
! Set highest compression level
PNGR,COMP,ON,9
! Set pixel resolution
/GFILE,2400
! Replot to file
/REPLOT
! Close file
/SHOW,CLOSE
! Make background black
/RGB,INDEX,0,0,0,0
! Make lines white
/RGB,INDEX,100,100,100,15

/REPLOT

PLNSOL,BFE,TEMP

! Make background white
/RGB,INDEX,100,100,100,0
! Make lines black
/RGB,INDEX,0,0,0,15
 ! Change output to file
/SHOW,PNG
! Set highest compression level
PNGR,COMP,ON,9
! Set pixel resolution
/GFILE,2400
! Replot to file
/REPLOT
! Close file
/SHOW,CLOSE
! Make background black
/RGB,INDEX,0,0,0,0
! Make lines white
/RGB,INDEX,100,100,100,15

/REPLOT
